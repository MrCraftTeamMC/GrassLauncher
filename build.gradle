plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id 'de.undercouch.download' version '5.5.0' apply false
}

allprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"

    group = rootProject.maven_group
    version = rootProject.release_version
    base.archivesName = rootProject.archives_base_name + "-" + project.name

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }

    tasks.withType(JavaCompile).configureEach {
        // ensure that the encoding is set to UTF-8, no matter what the system default is
        // this fixes some edge cases with special characters not displaying correctly // see [http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html](http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html)
        // If Javadoc is generated, this must be specified in that task too.
        it.options.encoding = "UTF-8"
//        it.options.release = 8
    }

    tasks.withType(Javadoc).configureEach {
        it.options.encoding = 'UTF-8'
    }

    tasks.withType(Test).configureEach {
        it.systemProperty "file.encoding", "UTF-8"
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
//        publications {
//            MrCraftTeamMCMaven(MavenPublication) {
//                from components.java
//            }
//        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/MrCraftTeamMC/GrassLauncher"
                credentials {
                    username = project.findProperty("gh_maven_username") ?: System.getenv("USERNAME")
                    password = project.findProperty("gh_maven_password") ?: System.getenv("TOKEN")
                }
            }
            // Jitpack
        }
    }
}

subprojects {
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "de.undercouch.download"

    configurations {
    }

    dependencies {
        // Annotations
        annotationProcessor(compileOnly("org.projectlombok:lombok:1.18.30"))
        compileOnly 'org.jetbrains:annotations:24.1.0'
        // Logging
        implementation "org.apache.logging.log4j:log4j-api:2.22.1"
        implementation "org.apache.logging.log4j:log4j-core:2.22.1"
        implementation "org.apache.logging.log4j:log4j-slf4j-impl:2.22.1"

        // Common
        implementation "com.github.KenRouKoro:ctoml4j:1.0.1"
        implementation "com.google.code.gson:gson:2.10.1"
//    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.0-rc1'
        implementation "com.squareup.okhttp3:okhttp:4.12.0"
        implementation "commons-io:commons-io:2.15.1"

        // Tests
        annotationProcessor(testCompileOnly("org.projectlombok:lombok:1.18.30"))
        testImplementation platform('org.junit:junit-bom:5.10.2')
        testImplementation 'org.junit.jupiter:junit-jupiter'
    }

    test {
        useJUnitPlatform()
    }
}